// This buildscript section is needed for Gradle to work with the Flyway plugin,
// so that the Flyway can connect to MySQL when we run Flyway commands from Gradle
// on command line.

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.flywaydb:flyway-mysql:10.10.0'
	}
}

plugins {
	id 'org.springframework.boot' version '3.2.3'
	id "io.spring.dependency-management" version "1.1.4"
	id "org.flywaydb.flyway" version "10.10.0"
	id 'java'
	id 'idea'
	id 'eclipse'
}

group = 'com.tsys'
version = '0.0.1'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

eclipse {
	classpath {
		downloadJavadoc = true
		downloadSources = true
	}
}

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	compileOnly 'org.projectlombok:lombok'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'mysql:mysql-connector-java:8.0.33'
	implementation 'org.flywaydb:flyway-core:10.10.0'
	implementation 'org.flywaydb:flyway-mysql:10.10.0'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'com.h2database:h2'
}

test {
	useJUnitPlatform()
}

flyway {
	url = 'jdbc:mysql://localhost:3306/flywaydemo?allowPublicKeyRetrieval=true&useSSL=false'
	user = 'springuser'
	password = 'ThePassword'
	schemas = ['flywaydemo']
	table = 'schema_version'
	driver = 'com.mysql.cj.jdbc.Driver'
//	placeholders = [
//			'keyABC': 'valueXYZ',
//			'otherplaceholder': 'value123'
//	]
}

// To reload on changing a static file, we need a way to copy
// the changed files into the classpath of the running app.
task reload(type: Copy) {
	from 'src/main/resources'
	into 'build/resources/main'
	include 'static/**'
	include 'templates/**'
	include 'custom/**'
}

// To pass arguments while running our application using Gradle
// Plugin, we need to configure our bootRun task:
// Usage on command line:
// gradle bootRun -Dserver.port=10002 -DcustomArgument=custom
bootRun {
	systemProperties = System.properties
}
